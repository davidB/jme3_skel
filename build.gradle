buildscript {
	repositories {
		jcenter()
		mavenLocal()
	}
	dependencies {
		classpath "net.alchim31.gradle:gradle-getdown-plugin:0.4.0"
	}
}
//apply plugin: 'application'
apply plugin: 'net.alchim31.getdown'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


version = '0.2.1'
group = 'net.alchim31.jme3'
mainClassName = 'jme3_skel.Main'
applicationDefaultJvmArgs = ['-ea']

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "http://updates.jmonkeyengine.org/maven/"}
	maven { url "http://dl.bintray.com/jmonkeyengine/contrib" }
}

def v_jme = '3.0.10' //'3.1.0-snapshot-github'
def v_slf4j = '1.7.7'

dependencies {
	compile 'org.projectlombok:lombok:1.14.2'
	compile "com.jme3:jme3-core:${v_jme}"
	compile "org.slf4j:slf4j-api:${v_slf4j}"
	//compile "com.jme3x:jfx:1.20140928141716.3a27fb5"
	compile "com.jme3x:jfx:1.+"
	compile 'com.squareup.dagger:dagger:1.2.2'
	compile 'com.squareup.dagger:dagger-compiler:1.2.2'
	compile 'com.netflix.rxjava:rxjava-core:0.19.2'
	compile 'net.alchim31.jme3:jme3_ext:0.1.0'
	runtime 'net.alchim31.jme3:jme3_ext:0.1.0:textures'
	runtime "com.jme3:jme3-lwjgl:${v_jme}"
	runtime("com.jme3:jme3-desktop:${v_jme}"){
		exclude module: "jinput"
	}
	runtime 'net.java.jinput:jinput:2.0.6'
	runtime "com.jme3:jme3-jogg:${v_jme}"
	//runtime "org.slf4j:slf4j-jdk14:${v_slf4j}"
	compile "org.slf4j:jul-to-slf4j:${v_slf4j}"
	runtime 'ch.qos.logback:logback-classic:1.1.2'
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-core:1.8.5'
}

sourceSets {
	main {
		resources {
			srcDirs = ["$projectDir/src/main/resources", "$projectDir/src/main/assets", "$projectDir/assets"]
			exclude "**/*.blend*"
		}
	}
}

task copyDeps(type: Copy) {
	description 'copy runtime + testRuntime dependencies into lib/ dir to allow other tools (jme3-SDK, eclipse apt-processor) to use libs'
	from (configurations.runtime + configurations.testCompile)
	into 'lib'
}


import bundles.Platform
getdown {
	urlbase = "http://alchim31.net/download/jme3_skel"
	mainClassName = mainClassName
	jvmArgs = applicationDefaultJvmArgs
	checklatest = false
	//platforms = [Platform.LINUX_I586, Platform.LINUX_X64, Platform.WINDOWS_I586] //Platform.values()
	//platforms = [Platform.LINUX_I586] //Platform.values()
	tmplGetdownTxt = tmplGetdownTxt + "\nallow_offline = true"
}
//project.afterEvaluate {
//	project.getdown.distSpec.eachFile {
//		println "$it.name"
//	}
//}
